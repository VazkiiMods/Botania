buildscript {
    repositories {
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath "com.diffplug.spotless:spotless-plugin-gradle:5.12.5"
    }
}
/* TODO someone please fix this I hate gradle so much
        I fixed it - Jared
*/

subprojects {
    apply plugin: 'java'
    apply plugin: 'pmd'
    apply plugin: "com.diffplug.spotless"
    apply plugin: 'maven-publish'
    java.toolchain.languageVersion = JavaLanguageVersion.of(17)
    java.withSourcesJar()
    archivesBaseName = "${mod_name}"

    repositories {
        mavenCentral()

        maven {
            name = 'Sponge / Mixin'
            url = 'https://repo.spongepowered.org/repository/maven-public/'
        }
    }

    tasks.withType(JavaCompile).configureEach {
        it.options.encoding = 'UTF-8'
        it.options.release = 17
        it.options.compilerArgs << "-Xmaxerrs" << "1000"
    }

    tasks.withType(GenerateModuleMetadata).configureEach {
        it.enabled = false
    }

    pmd {
        toolVersion '6.35.0'
        incrementalAnalysis.set(true)
        ruleSets.clear()
        ruleSetFiles = rootProject.files("spotless/pmd-ruleset.xml")
    }

    jar {
        from "LICENSE"

        exclude "**/*.bat"
        exclude "**/*.psd"
        exclude "**/*.exe"
        exclude "**/unused"

        exclude ".cache"
    }

    spotless {
        enforceCheck = false // workaround around jenkins having wrong line endings in some files
        // we explicitly check in github actions builds
        java {
            indentWithTabs()
            eclipse().configFile rootProject.file('spotless/eclipseformat.xml')
            removeUnusedImports()
            importOrderFile rootProject.file('spotless/eclipse.importorder')
        }
    }
}

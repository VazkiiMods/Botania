plugins {
    id 'fabric-loom' version '0.4-SNAPSHOT'
    id 'maven-publish'
    id "com.diffplug.spotless" version "5.7.0"
}

apply plugin: 'pmd'

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8


ext.configFile = file('build.properties')
ext.config = parseConfig(configFile)
ext.priv = parseConfig(file('private.properties'))

def mc_version = "1.16.3"
version = "${mc_version}-${config.build_number}-FABRIC"
if (System.getenv().RELEASE_MODE == null) {
    version += '-SNAPSHOT'
}

group = "vazkii.${config.mod_id}"
archivesBaseName = "${config.mod_name}"

compileJava.options.compilerArgs << "-Xlint:all,-classfile,-processing,-deprecation" << "-Werror"

minecraft {
    accessWidener "src/main/resources/botania.accesswidener"
}

sourceSets.main.resources {
    srcDir 'src/generated/resources'
}

repositories {
    maven {
        name = "Modmuss"
        url = "https://maven.modmuss50.me/"
    }
    maven {
        name = "Jared"
        url = "https://maven.blamejared.com/"
    }
    maven {
        url = "https://maven.theillusivec4.top/"
    }
    maven {
          name = "Ladysnake Libs"
	      url = 'https://dl.bintray.com/ladysnake/libs'
    }
    maven {
        name "storagedrawers"
        url = "https://dl.bintray.com/jaquadro/dev/"
    }
    maven {
        name = "BuildCraft"
        url = "https://mod-buildcraft.com/maven"
    }
    maven {
        name "entity reach"
        url "https://maven.jamieswhiteshirt.com/libs-release/"
    }
}

dependencies {
    compileOnly "com.google.code.findbugs:jsr305:3.0.2"

    // modCompile "vazkii.patchouli:Patchouli:1.16.2-44-FABRIC:api"
    modImplementation "vazkii.patchouli:Patchouli:1.16.2-44-FABRIC"

    modCompile "alexiil.mc.lib:libblockattributes-all:0.8.4"

    modImplementation "me.zeroeightsix:fiber:0.23.0-2"
    include "me.zeroeightsix:fiber:0.23.0-2"

    // Match version with curios, don't include as curios includes it already
    modImplementation "io.github.onyxstudios.Cardinal-Components-API:cardinal-components-base:2.7.2"
    modImplementation "io.github.onyxstudios.Cardinal-Components-API:cardinal-components-block:2.7.2"
    modImplementation "io.github.onyxstudios.Cardinal-Components-API:cardinal-components-entity:2.7.2"
    modImplementation "io.github.onyxstudios.Cardinal-Components-API:cardinal-components-util:2.7.2"
    compileOnly "com.demonwav.mcdev:annotations:1.0" // only to shut up compile warnings from CCA's usage of CheckEnv and Env
    modImplementation "top.theillusivec4.curios:curios-fabric:0.0.10-1.16.3"
    modImplementation "com.jamieswhiteshirt:reach-entity-attributes:1.0.1"
    include "com.jamieswhiteshirt:reach-entity-attributes:1.0.1"

    minecraft "com.mojang:minecraft:1.16.3"
    mappings "net.fabricmc:yarn:1.16.3+build.47:v2"
    modCompile "net.fabricmc:fabric-loader:0.10.6+build.214"

    // Fabric API. This is technically optional, but you probably want it anyway.
    modCompile "net.fabricmc.fabric-api:fabric-api:0.26.0+1.16"

    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.6.1'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.6.1'
    // PSA: Some older mods, compiled on Loom 0.2.1, might have outdated Maven POMs.
    // You may need to force-disable transitiveness on them.
}

spotless {
    enforceCheck = false // workaround around jenkins having wrong line endings in some files
                         // we explicitly check in github actions builds
    java {
        indentWithTabs()
        licenseHeaderFile('spotless/license_template.txt')
        eclipse().configFile 'spotless/eclipseformat.xml'
        removeUnusedImports()
        importOrderFile 'spotless/eclipse.importorder'
    }
}

pmd {
    toolVersion '6.22.0'
    incrementalAnalysis.set(true)
    ruleSets.clear()
    ruleSetFiles = files("spotless/pmd-ruleset.xml")
}

task checkSyntax (group: "verification") {
    dependsOn spotlessJavaCheck, pmdMain
}

processResources {
    inputs.property "version", project.version

    from(sourceSets.main.resources.srcDirs) {
        include "fabric.mod.json"
        expand "version": project.version
    }

    from(sourceSets.main.resources.srcDirs) {
        exclude "fabric.mod.json"
    }

    // Exclude datagenerator .cache directory
    exclude '.cache'

    filesMatching('data/botania/patchouli_books/lexicon/book.json') {
        filter {
            it.replaceAll("buildNumber", config.build_number)
        }
    }
}

test {
    useJUnitPlatform()
}

task incrementBuildNumber {
    doFirst {
        config.build_number = (config.build_number.toString().toInteger()) + 1
        configFile.withWriter {
            config.toProperties().store(it, "")
        }
    }
}

def parseConfig(File config) {
	if(config.exists()){
		config.withReader {
			def prop = new Properties()
			prop.load(it)
			return (new ConfigSlurper().parse(prop))
		}
	} else {
		return null
	}
}

// ensure that the encoding is set to UTF-8, no matter what the system default is
// this fixes some edge cases with special characters not displaying correctly
// see http://yodaconditions.net/blog/fix-for-java-file-encoding-problems-with-gradle.html
tasks.withType(JavaCompile) {
    options.encoding = "UTF-8"
}

// Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
// if it is present.
// If you remove this task, sources will not be generated.
task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = "sources"
    from sourceSets.main.allSource
}

jar {
    from "LICENSE"

    exclude "**/*.bat"
    exclude "**/*.psd"
    exclude "**/*.exe"
}

task apiJar(type: Jar, dependsOn: classes) {
    from(sourceSets.main.output)
    include 'vazkii/botania/api/**'

    classifier = "api"
}

// configure the maven publication
publishing {
    publications {
        mavenJava(MavenPublication) {
            // add all the jars that should be included when publishing to maven
            artifact(remapJar) {
                builtBy remapJar
            }
            artifact(sourcesJar) {
                builtBy remapSourcesJar
            }
            artifact(apiJar) {
                builtBy apiJar
            }
        }
    }

    // select the repositories you want to publish to
    repositories {
        maven {
            url "file://" + System.getenv("local_maven")
        }
    }
}

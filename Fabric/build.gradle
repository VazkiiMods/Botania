plugins {
    id 'fabric-loom' version '1.3.8'
    id 'io.github.juuxel.loom-quiltflower' version '1.7.1'
}

version = "${minecraft_version}-${build_number}-FABRIC"
if (System.getenv().RELEASE_MODE == null) {
    version += '-SNAPSHOT'
}

loom {
    accessWidenerPath = file("src/main/resources/botania.accesswidener")
    mixin.defaultRefmapName = "botania.refmap.json"

    runs {
        gameTest {
            server()
            vmArg "-Dfabric-api.gametest=1"
        }

        xplatDatagen {
            client()
            vmArg "-Dbotania.xplat_datagen=1"
            vmArg "-Dfabric-api.datagen"
            vmArg "-Dfabric-api.datagen.modid=botania"
            vmArg "-Dfabric-api.datagen.output-dir=${file("../Xplat/src/generated/resources")}"
        }

        fabricDatagen {
            client()
            vmArg "-Dfabric-api.datagen"
            vmArg "-Dfabric-api.datagen.modid=botania"
            vmArg "-Dfabric-api.datagen.output-dir=${file("src/generated/resources")}"
        }

        configureEach {
            runDir "../run"
            ideConfigGenerated(true)
        }
    }

}

sourceSets.main.resources {
    srcDir 'src/generated/resources'
}

repositories {
    maven {
        name = "JEI"
        url "https://dvs1.progwml6.com/files/maven/"
    }
    maven {
        name = "Modmuss"
        url = "https://maven.modmuss50.me/"
    }
    maven {
        name = "Jared"
        url = "https://maven.blamejared.com/"
    }
    maven {
        url = "https://maven.terraformersmc.com/" // for trinkets and emi
    }
    maven {
          name = "Ladysnake Libs"
          url = 'https://maven.ladysnake.org/releases'
    }
    maven {
        name = "BuildCraft" // for LBA
        url = "https://mod-buildcraft.com/maven"
    }
    maven {
        name "entity reach"
        url "https://maven.jamieswhiteshirt.com/libs-release/"
    }
    maven {
        url "https://jitpack.io" // for step height eam
    }
    maven {
        url "https://maven.shedaniel.me/" // for rei
    }
    maven {
        name = "ParchmentMC"
        url = "https://maven.parchmentmc.net/"
    }
    maven {
        name = "Unascribed"
        url "https://repo.unascribed.com" // for ears
        content {
            includeGroup "com.unascribed"
        }
    }
    maven {
        name = "Modrinth"
        url = "https://api.modrinth.com/maven" // for "here be no dragons"
        content {
            includeGroup "maven.modrinth"
        }
    }
}

dependencies {
    minecraft "com.mojang:minecraft:${minecraft_version}"
    mappings loom.layered() {
        officialMojangMappings()
        parchment("org.parchmentmc.data:parchment-1.19.2:2022.11.27@zip")
    }

    modImplementation "net.fabricmc:fabric-loader:0.14.11"
    modImplementation "net.fabricmc.fabric-api:fabric-api:0.73.0+1.19.2"

    compileOnly project(":Xplat")

    modImplementation "vazkii.patchouli:Patchouli:${minecraft_version}-76-FABRIC"

    modCompileOnly "me.shedaniel:RoughlyEnoughItems-fabric:9.1.528"

    modImplementation("dev.emi:emi:0.5.0+1.19.2") { transitive = false }

    modImplementation "me.zeroeightsix:fiber:0.23.0-2"
    include "me.zeroeightsix:fiber:0.23.0-2"

    // Don't include base and entity as Trinkets includes it already
    modImplementation "dev.onyxstudios.cardinal-components-api:cardinal-components-base:5.0.0-beta.1"
    modImplementation "dev.onyxstudios.cardinal-components-api:cardinal-components-entity:5.0.0-beta.1"

    compileOnly "com.demonwav.mcdev:annotations:1.0" // only to shut up compile warnings from CCA's usage of CheckEnv and Env

    modImplementation("dev.emi:trinkets:3.4.0") { transitive = false }

    modImplementation "com.jamieswhiteshirt:reach-entity-attributes:2.3.0"
    include "com.jamieswhiteshirt:reach-entity-attributes:2.3.0"

    modImplementation "com.github.emilyploszaj:step-height-entity-attribute:v1.0.1"
    include "com.github.emilyploszaj:step-height-entity-attribute:v1.0.1"

    modCompileOnly 'teamreborn:energy:2.1.0'

    // modCompileOnly 'com.blamejared.crafttweaker:CraftTweaker-fabric-1.18.2:9.1.123'

    modCompileOnly 'com.unascribed:ears-api:1.4.5'

    modCompileOnly "mezz.jei:jei-1.19.2-common-api:11.2.0.247"

    modRuntimeOnly "maven.modrinth:here-be-no-dragons:1.0.0"
    modRuntimeOnly "maven.modrinth:lazydfu:0.1.3"
}

compileJava {
	source(project(":Xplat").sourceSets.main.allSource)
	options.compilerArgs << "-Xlint:all,-classfile,-processing,-deprecation,-serial" << "-Werror"

}

sourcesJar {
    from project(":Xplat").sourceSets.main.allJava
}

processResources {
    from project(":Xplat").sourceSets.main.resources
    inputs.property "version", project.version

    filesMatching("fabric.mod.json") {
        expand "version": project.version
    }

    // Exclude datagenerator .cache directory
    exclude '.cache'

    filesMatching('data/botania/patchouli_books/lexicon/book.json') {
        filter {
            it.replaceAll("buildNumber", build_number)
        }
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId project.group
            artifactId project.archivesBaseName
            version project.version
            from components.java
        }
    }

    repositories {
        maven {
            url "file://" + System.getenv("local_maven")
        }
    }
}

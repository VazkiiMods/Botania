plugins {
    id 'fabric-loom' version '1.3.8'
    id 'io.github.juuxel.loom-quiltflower' version '1.7.1'
}

version = "${minecraft_version}-${build_number}-FABRIC"
if (System.getenv().RELEASE_MODE != "1") {
    version += '-SNAPSHOT'
}

loom {
    accessWidenerPath = file("src/main/resources/botania.accesswidener")
    mixin.defaultRefmapName = "botania.refmap.json"

    runs {
        gameTest {
            server()
            vmArg "-Dfabric-api.gametest=1"
        }

        xplatDatagen {
            client()
            vmArg "-Dbotania.xplat_datagen=1"
            vmArg "-Dfabric-api.datagen"
            vmArg "-Dfabric-api.datagen.modid=botania"
            vmArg "-Dfabric-api.datagen.output-dir=${file("../Xplat/src/generated/resources")}"
        }

        fabricDatagen {
            client()
            vmArg "-Dfabric-api.datagen"
            vmArg "-Dfabric-api.datagen.modid=botania"
            vmArg "-Dfabric-api.datagen.output-dir=${file("src/generated/resources")}"
        }

        configureEach {
            runDir "../run"
            ideConfigGenerated(true)
        }
    }

}

sourceSets.main.resources {
    srcDir 'src/generated/resources'
}

repositories {
    maven {
        name = "Modmuss"
        url = "https://maven.modmuss50.me/"
    }
    maven {
        name = "Jared"
        url = "https://maven.blamejared.com/" // for JEI and Patchouli
    }
    maven {
        name = "TerraformersMC"
        url = "https://maven.terraformersmc.com/" // for trinkets and emi
    }
    maven {
          name = "Ladysnake Libs"
          url = 'https://maven.ladysnake.org/releases'
    }
    maven {
        name = "BuildCraft" // for LBA
        url = "https://mod-buildcraft.com/maven"
    }
    maven {
        name "entity reach"
        url "https://maven.jamieswhiteshirt.com/libs-release/"
    }
    maven {
        url "https://jitpack.io" // for step height eam
    }
    maven {
        url "https://maven.shedaniel.me/" // for rei
    }
    maven {
        name = "ParchmentMC"
        url = "https://maven.parchmentmc.net/"
    }
    maven {
        name = "Unascribed"
        url "https://repo.unascribed.com" // for ears
        content {
            includeGroup "com.unascribed"
        }
    }
    maven {
        name = "Modrinth"
        url = "https://api.modrinth.com/maven" // for "here be no dragons"
        content {
            includeGroup "maven.modrinth"
        }
    }
}

dependencies {
    minecraft "com.mojang:minecraft:${minecraft_version}"
    mappings loom.layered() {
        officialMojangMappings()
        parchment("org.parchmentmc.data:parchment-1.19.3:2023.03.12@zip")
    }

    modImplementation "net.fabricmc:fabric-loader:0.14.21"
    modImplementation "net.fabricmc.fabric-api:fabric-api:0.83.1+1.20.1"

    compileOnly project(":Xplat")

    modImplementation "vazkii.patchouli:Patchouli:${minecraft_version}-84-FABRIC"

    modCompileOnly "me.shedaniel:RoughlyEnoughItems-fabric:12.0.625"

    modCompileOnly "dev.emi:emi-fabric:1.0.12+${minecraft_version}:api"
    modLocalRuntime "dev.emi:emi-fabric:1.1.4+${minecraft_version}"

    modImplementation "me.zeroeightsix:fiber:0.23.0-2"  
    include "me.zeroeightsix:fiber:0.23.0-2"

    // Don't include base and entity as Trinkets includes it already
    modImplementation "dev.onyxstudios.cardinal-components-api:cardinal-components-base:5.2.1"
    modImplementation "dev.onyxstudios.cardinal-components-api:cardinal-components-entity:5.2.1"

    compileOnly "com.demonwav.mcdev:annotations:1.0" // only to shut up compile warnings from CCA's usage of CheckEnv and Env

    modImplementation("dev.emi:trinkets:3.7.0") { transitive = false }

    modImplementation "com.jamieswhiteshirt:reach-entity-attributes:2.4.0"
    include "com.jamieswhiteshirt:reach-entity-attributes:2.4.0"

    modImplementation "com.github.emilyploszaj:step-height-entity-attribute:v1.2.0"
    include "com.github.emilyploszaj:step-height-entity-attribute:v1.2.0"

    modCompileOnly 'teamreborn:energy:3.0.0'

    // modCompileOnly 'com.blamejared.crafttweaker:CraftTweaker-fabric-1.18.2:9.1.123'

    modCompileOnly 'com.unascribed:ears-api:1.4.5'

    compileOnly "mezz.jei:jei-1.20.1-common-api:15.2.0.27"

    modRuntimeOnly "maven.modrinth:here-be-no-dragons:1.0.0"
}

compileJava {
	source(project(":Xplat").sourceSets.main.allSource)
	options.compilerArgs << "-Xlint:all,-classfile,-processing,-deprecation,-serial" << "-Werror"

}

sourcesJar {
    from project(":Xplat").sourceSets.main.allJava
}

processResources {
    from project(":Xplat").sourceSets.main.resources
    inputs.property "version", project.version

    filesMatching("fabric.mod.json") {
        expand "version": project.version
    }

    // Exclude datagenerator .cache directory
    exclude '.cache'

    filesMatching('data/botania/patchouli_books/lexicon/book.json') {
        filter {
            it.replaceAll("buildNumber", build_number)
        }
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId project.group
            artifactId project.archivesBaseName
            version project.version
            from components.java
        }
    }

    repositories {
        maven {
            url "file://" + System.getenv("local_maven")
        }
    }
}

plugins {
    id 'fabric-loom' version '0.10-SNAPSHOT'
    id 'io.github.juuxel.loom-quiltflower-mini' version '1.2.1'
}

version = "${minecraft_version}-${build_number}-FABRIC"
if (System.getenv().RELEASE_MODE == null) {
    version += '-SNAPSHOT'
}

compileJava.options.compilerArgs << "-Xlint:all,-classfile,-processing,-deprecation,-serial" << "-Werror"

loom {
    accessWidenerPath = file("src/main/resources/botania.accesswidener")
    runs {
        gameTest {
            server()
            vmArg "-Dfabric-api.gametest=1"
        }

        datagen {
            client()
            vmArg "-Dfabric-api.datagen"
            vmArg "-Dfabric-api.datagen.modid=botania"
            vmArg "-Dfabric-api.datagen.output-dir=${file("src/generated/resources")}"
            vmArg "-Dfabric-api.datagen.strict-validation"
        }
    }

}

sourceSets.main.resources {
    srcDir 'src/generated/resources'
}

repositories {
    maven {
        name = "Modmuss"
        url = "https://maven.modmuss50.me/"
    }
    maven {
        name = "Jared"
        url = "https://maven.blamejared.com/"
    }
    maven {
        url = "https://maven.terraformersmc.com/" // for trinkets
    }
    maven {
          name = "Ladysnake Libs"
          url = 'https://ladysnake.jfrog.io/artifactory/mods'
    }
    maven {
        name = "BuildCraft" // for LBA
        url = "https://mod-buildcraft.com/maven"
    }
    maven {
        name "entity reach"
        url "https://maven.jamieswhiteshirt.com/libs-release/"
    }
    maven {
        url "https://jitpack.io" // for step height eam
    }
    maven {
        url "https://maven.shedaniel.me/" // for rei
    }
    maven {
        url "https://server.bbkr.space/artifactory/libs-release/" // for bannerplusplus
    }
    maven {
        name = "ParchmentMC"
        url = "https://maven.parchmentmc.net/"
    }
}

test {
    useJUnitPlatform()
}

dependencies {
    minecraft "com.mojang:minecraft:${minecraft_version}"
    mappings loom.layered() {
        officialMojangMappings()
        parchment("org.parchmentmc.data:parchment-1.18.1:2021.12.19@zip")
    }

    modImplementation "net.fabricmc:fabric-loader:0.12.8"
    modImplementation "net.fabricmc.fabric-api:fabric-api:0.44.0+1.18"

    compileOnly "com.google.code.findbugs:jsr305:3.0.2"
    implementation project(":Common")

    modImplementation "vazkii.patchouli:Patchouli:1.18.1-61-FABRIC"
    modImplementation "me.shedaniel:RoughlyEnoughItems-fabric:7.0.343"

    modImplementation "me.zeroeightsix:fiber:0.23.0-2"
    include "me.zeroeightsix:fiber:0.23.0-2"

    // Don't include base and entity as Trinkets includes it already
    modImplementation "io.github.onyxstudios.Cardinal-Components-API:cardinal-components-base:4.0.1"
    modImplementation "io.github.onyxstudios.Cardinal-Components-API:cardinal-components-entity:4.0.1"

    compileOnly "com.demonwav.mcdev:annotations:1.0" // only to shut up compile warnings from CCA's usage of CheckEnv and Env

    modImplementation "dev.emi:trinkets:3.1.0"

    modImplementation "com.jamieswhiteshirt:reach-entity-attributes:2.1.1"
    include "com.jamieswhiteshirt:reach-entity-attributes:2.1.1"

    modImplementation "com.github.emilyploszaj:step-height-entity-attribute:v1.0.1"
    include "com.github.emilyploszaj:step-height-entity-attribute:v1.0.1"

    /*
    modImplementation "io.github.fablabsmc:bannerpp:2.0.4+mc.1.17-rc1"
    include "io.github.fablabsmc:bannerpp:2.0.4+mc.1.17-rc1"
    */

    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.6.1'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.6.1'
}

tasks.withType(JavaCompile) {
	source(project(":Common").sourceSets.main.allSource)
}

processResources {
    from project(":Common").sourceSets.main.resources
    inputs.property "version", project.version

    filesMatching("fabric.mod.json") {
        expand "version": project.version
    }

    // Exclude datagenerator .cache directory
    exclude '.cache'

    // Exclude Forge-specific metadata on Fabric only
    exclude 'META-INF/mods.toml'
    exclude 'META-INF/accesstransformer.cfg'

    filesMatching('data/botania/patchouli_books/lexicon/book.json') {
        filter {
            it.replaceAll("buildNumber", build_number)
        }
    }
}
